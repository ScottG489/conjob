plugins {
    id 'java'
    id 'scala'
    id 'application'
    id 'org.ajoberstar.grgit' version '5.3.3'
    id "io.gatling.gradle" version "3.14.9.4"
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0'
}

group = 'conjob'
mainClassName = 'conjob.ConJobApplication'

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    compileOnly 'org.projectlombok:lombok:1.18.42'

    implementation 'io.dropwizard:dropwizard-core:4.0.16'
    implementation 'io.dropwizard:dropwizard-auth:4.0.16'

    implementation('com.spotify:docker-client:8.16.0:shaded') {
        exclude group: 'org.glassfish.jersey.core', module: 'jersey-client'
        exclude group: 'org.glassfish.jersey.connectors', module: 'jersey-apache-connector'
        exclude group: 'org.glassfish.jersey.media', module: 'jersey-media-json-jackson'
        exclude group: 'javax.ws.rs', module: 'javax.ws.rs-api'
    }
}

apply from: 'gradle/version.gradle'
apply from: 'gradle/unit-test.gradle'
apply from: 'gradle/integration-test.gradle'
apply from: 'gradle/acceptance-test.gradle'
apply from: 'gradle/performance-test.gradle'

repositories {
    mavenCentral()
}

task jacocoAllTestReport(type: JacocoReport) {
    getExecutionData().setFrom(fileTree(buildDir)
            .include("/jacoco/unitTest.exec")
            .include("/jacoco/integrationTest.exec")
            .include("/jacoco/run.exec"))
    sourceSets sourceSets.main
}

jacoco {
    applyTo run
}

task jacocoApplicationReport(type: JacocoReport) {
    executionData run
    sourceSets sourceSets.main
}
