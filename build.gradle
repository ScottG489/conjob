plugins {
    id 'java'
    id 'scala'
    id 'application'
    id 'jacoco'
    id 'org.ajoberstar.grgit' version '5.3.3'
    id "io.gatling.gradle" version "3.14.9.8"
    id 'info.solidsoft.pitest' version '1.19.0-rc.3'
}

group = 'conjob'

application {
    mainClass = 'conjob.ConJobApplication'
}

run {
    args 'server', 'local-config.yml'
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    compileOnly 'org.projectlombok:lombok:1.18.42'

    implementation 'io.dropwizard:dropwizard-core:5.0.1'
    implementation 'io.dropwizard:dropwizard-auth:5.0.1'

    implementation 'com.github.docker-java:docker-java-core:3.7.0'
    implementation 'com.github.docker-java:docker-java-transport-httpclient5:3.7.0'
}

apply from: 'gradle/version.gradle'
apply from: 'gradle/unit-test.gradle'
apply from: 'gradle/integration-test.gradle'
apply from: 'gradle/acceptance-test.gradle'
apply from: 'gradle/performance-test.gradle'

repositories {
    mavenCentral()
}

task jacocoAllTestReport(type: JacocoReport) {
    getExecutionData().setFrom(fileTree(buildDir)
            .include("/jacoco/unitTest.exec")
            .include("/jacoco/integrationTest.exec")
            .include("/jacoco/run.exec"))
    sourceSets sourceSets.main
}

jacoco {
    applyTo run
}

task jacocoApplicationReport(type: JacocoReport) {
    executionData run
    sourceSets sourceSets.main
}
