sourceSets {
    unitTest {
        java.srcDirs = ['src/test/unit/java']
        resources.srcDir file('src/test/unit/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    unitTestRuntimeClasspath.extendsFrom runtimeClasspath
}

ext.unitTestJqwikVersion = '1.9.3'

pitest {
    testSourceSets.set([sourceSets.unitTest])
    junit5PluginVersion = '0.15'
    timestampedReports = false
    // If you want to run all mutators: https://pitest.org/quickstart/mutators/
//    mutators = ["ALL"]
    // If you want to run specific tests (faster)
//    targetTests = ['*JobServiceTest']
}

task unitTest(type: Test) {
    testClassesDirs = sourceSets.unitTest.output.classesDirs
    classpath = sourceSets.unitTest.runtimeClasspath

    useJUnitPlatform()

    mustRunAfter build
}

dependencies {
    unitTestAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
    unitTestCompileOnly 'org.projectlombok:lombok:1.18.42'

    unitTestImplementation "org.mockito:mockito-core:5.21.0"
    unitTestImplementation 'org.mockito:mockito-inline:5.2.0'
    unitTestImplementation platform('org.junit:junit-bom:5.14.1')
    unitTestImplementation 'org.junit.jupiter:junit-jupiter'
    unitTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    unitTestImplementation 'org.hamcrest:hamcrest:3.0'
    unitTestImplementation "net.jqwik:jqwik:${unitTestJqwikVersion}"
    unitTestImplementation 'com.tngtech.archunit:archunit:0.23.1'
    unitTestImplementation 'com.tngtech.archunit:archunit-junit5:1.4.1'
}

task jacocoUnitTestReport(type: JacocoReport) {
    dependsOn unitTest
    executionData unitTest
    sourceSets sourceSets.main
}

unitTest.finalizedBy jacocoUnitTestReport
