sourceSets {
    unitTest {
        java.srcDirs = ['src/test/unit/java']
        resources.srcDir file('src/test/unit/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

ext.jqwikVersion = '1.5.1'

pitest {
    testSourceSets.set([sourceSets.unitTest])
    junit5PluginVersion = '0.12'
    timestampedReports = false
}

task unitTest(type: Test) {
    testClassesDirs = sourceSets.unitTest.output.classesDirs
    classpath = sourceSets.unitTest.runtimeClasspath

    useJUnitPlatform()

    mustRunAfter build
}

dependencies {
    testCompile "org.mockito:mockito-core:3.8.0"
    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation "net.jqwik:jqwik:${jqwikVersion}"
    testImplementation 'com.tngtech.archunit:archunit:0.17.0'
    testImplementation 'com.tngtech.archunit:archunit-junit5:0.17.0'
}

task jacocoUnitTestReport(type:JacocoReport) {
    dependsOn unitTest
    executionData unitTest
    sourceSets sourceSets.main
}

unitTest.finalizedBy jacocoUnitTestReport
