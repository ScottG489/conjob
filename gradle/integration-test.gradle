sourceSets {
    integrationTest {
        java.srcDirs = ['src/test/integration/java']
        resources.srcDir file('src/test/integration/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

ext.integrationTestJqwikVersion = '1.5.1'

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()

    mustRunAfter build
}

dependencies {
    integrationTestImplementation platform('org.junit:junit-bom:5.14.1')
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter'
    integrationTestImplementation 'org.hamcrest:hamcrest:2.2'
    integrationTestImplementation "net.jqwik:jqwik:${integrationTestJqwikVersion}"
    integrationTestImplementation 'io.rest-assured:rest-assured:4.5.1'
}

task jacocoIntegrationTestReport(type: JacocoReport) {
    dependsOn integrationTest
    executionData integrationTest
    sourceSets sourceSets.main
}

integrationTest.finalizedBy jacocoIntegrationTestReport
